/* multi-aspect-signals.nml */

/*
    License: GPL v2
*/

grf {
    grfid: "JGR\81";
    name: string(STR_GRF_NAME);
    desc: string(STR_GRF_DESCRIPTION);
    version: 5;
    min_compatible_version: 0;
    url: string(STR_GRF_URL);

    param {
        param_block_style {
            type: int;
            name: string(STR_PARAM_BLOCK_STYLE_NAME);
            desc: string(STR_PARAM_BLOCK_STYLE_DESC);
            min_value: 0;
            max_value: 1;
            def_value: 0;
            names: {
                0: string(STR_BLOCK_STYLE_TRADITIONAL);
                1: string(STR_BLOCK_STYLE_NEW);
            };
        }
    }
    param {
        param_routing_restriction {
            type: bool;
            name: string(STR_PARAM_ROUTING_RESTRICTIONS_NAME);
            desc: string(STR_PARAM_ROUTING_RESTRICTIONS_DESC);
            def_value: 1;
        }
    }
    param {
        param_aspect_style {
            type: int;
            name: string(STR_PARAM_ASPECT_STYLE_NAME);
            desc: string(STR_PARAM_ASPECT_STYLE_DESC);
            min_value: 0;
            max_value: 2;
            def_value: 1;
            names: {
                0: string(STR_ASPECT_STYLE_3);
                1: string(STR_ASPECT_STYLE_4);
                2: string(STR_ASPECT_STYLE_4_JAPANESE);
            };
        }
    }
    param {
        param_custom_styles {
            type: bool;
            name: string(STR_PARAM_CUSTOM_STYLES_NAME);
            desc: string(STR_PARAM_CUSTOM_STYLES_DESC);
            def_value: 0;
        }
    }
    param {
        param_pbs_height {
            type: int;
            name: string(STR_PARAM_PBS_HEIGHT_NAME);
            desc: string(STR_PARAM_PBS_HEIGHT_DESC);
            min_value: 0;
            max_value: 2;
            def_value: 0;
            names: {
                0: string(STR_PBS_HEIGHT_NORMAL);
                1: string(STR_PBS_HEIGHT_CLEARANCE);
                2: string(STR_PBS_HEIGHT_SHORT);
            };
        }
    }
}

if (!extended_feature_test("action3_signals_custom_signal_sprites")) {
    error(FATAL, string(STR_UNSUPPORTED_VERSION));
}

param[10] = reserve_sprites(3);
replace(param[10]) {
    recolour_sprite {
        0x03..0x09: 0xC6..0xCC;
        0x20..0x21: 0xC9..0xCA;
        0x28: 0xCB;
        0x6A: 0xC8;
        0x88: 0xC6;
        0xF5: 0xCC;
    }
    recolour_sprite {
        0xAB..0xAE: 0x83..0x86;
    }
    recolour_sprite {
        0xAB..0xAE: 0x94..0x97;
        //0x81..0x86: 0x92..0x97;
    }
}

if (param_block_style == 1 || param_pbs_height == 0) {
    param[11] = 0;
} else {
    if (param_pbs_height == 2) {
        param[11] = 0x100;
    } else {
        if (extended_feature_test("varaction2_signals_signal_vertical_clearance")) {
            param[11] = 14;
        } else {
            param[11] = 0;
        }
    }
}

if (param_block_style == 1 && param_pbs_height != 0) {
    error(NOTICE, string(STR_UNSUPPORTED_PBS_HEIGHT_COMBINATION));
} else {
    if (param_pbs_height == 1 && !extended_feature_test("varaction2_signals_signal_vertical_clearance")) {
        error(NOTICE, string(STR_UNSUPPORTED_VERTICAL_CLEARANCE));
    }
}

template tmpl_signals(x,y) {
    [x +   0, y, 20, 30, -9, -28] // ne-sw
    [x +  60, y, 20, 30, -9, -28] // sw-ne
    [x + 120, y, 20, 30, -9, -28] // se-nw
    [x + 180, y, 20, 30, -9, -28] // nw-se
    [x + 300, y, 20, 30, -9, -28] // e-w
    [x + 240, y, 20, 30, -9, -28] // w-e
    [x + 360, y, 20, 30, -9, -28] // s-n
    [x + 420, y, 20, 30, -9, -28] // n-s
}

template tmpl_signals_combined(x, y) {
    [x +   0, y, 20, 30,  -9, -29] // ne-sw
    [x +  60, y, 20, 30,  -9, -29] // sw-ne
    [x + 120, y, 20, 30,  -9, -29] // se-nw
    [x + 180, y, 20, 30,  -9, -29] // nw-se
    [x + 300, y, 20, 30,  -9, -29] // e-w
    [x + 240, y, 20, 30,  -9, -29] // w-e
    [x + 360, y, 20, 30, -10, -29] // s-n
    [x + 420, y, 20, 30, -10, -29] // n-s
}

template tmpl_pbs_signals(x,y) {
    [x +   0, y, 5, 25, -2, -23] // ne-sw
    [x +  32, y, 4, 25, -1, -23] // sw-ne
    [x +  64, y, 4, 25, -1, -23] // se-nw
    [x +  96, y, 5, 25, -1, -23] // nw-se
    [x + 128, y, 5, 25, -1, -23] // e-w
    [x + 160, y, 5, 25, -2, -23] // w-e
    [x + 191, y, 6, 25, -3, -23] // s-n
    [x + 223, y, 6, 25, -3, -23] // n-s
}

spriteset (signals_block_red,   "gfx/light-generic.png") { tmpl_signals(10, 50) }
spriteset (signals_block_green, "gfx/light-generic.png") { tmpl_signals(40, 50) }

spriteset (signals_block_y,     "gfx/light-generic.png") { tmpl_signals(10, 100) }
spriteset (signals_block_yy,    "gfx/light-generic.png") { tmpl_signals(40, 100) }

spriteset (signals_pbs_red,     "gfx/light-generic.png") { tmpl_pbs_signals(10, 147) }
spriteset (signals_pbs_green,   "gfx/light-generic.png") { tmpl_pbs_signals(26, 147) }

spriteset (signals_pbs_oneway_red,   "gfx/light-generic.png") { tmpl_pbs_signals(10, 178) }
spriteset (signals_pbs_oneway_green, "gfx/light-generic.png") { tmpl_pbs_signals(26, 178) }

spriteset (signals_pbs_y,            "gfx/light-generic.png") { tmpl_pbs_signals(10, 209) }
spriteset (signals_pbs_yy,           "gfx/light-generic.png") { tmpl_pbs_signals(26, 209) }

spriteset (signals_pbs_oneway_y,     "gfx/light-generic.png") { tmpl_pbs_signals(10, 240) }
spriteset (signals_pbs_oneway_yy,    "gfx/light-generic.png") { tmpl_pbs_signals(26, 240) }

spriteset (signals_new_block_red,    "gfx/light-generic.png") { tmpl_pbs_signals(10, 271) }
spriteset (signals_new_block_green,  "gfx/light-generic.png") { tmpl_pbs_signals(26, 271) }

spriteset (signals_new_block_y,      "gfx/light-generic.png") { tmpl_pbs_signals(10, 302) }
spriteset (signals_new_block_yy,     "gfx/light-generic.png") { tmpl_pbs_signals(26, 302) }

spriteset (signals_pbs_oneway_red_short,   "gfx/light-generic.png") { tmpl_pbs_signals(10, 332) }
spriteset (signals_pbs_oneway_green_short, "gfx/light-generic.png") { tmpl_pbs_signals(26, 332) }

spriteset (signals_pbs_oneway_y_short,     "gfx/light-generic.png") { tmpl_pbs_signals(10, 363) }
spriteset (signals_pbs_oneway_yy_short,    "gfx/light-generic.png") { tmpl_pbs_signals(26, 363) }

spriteset (signals_shunt_red,   "gfx/shunt.png") { tmpl_signals(10, 50) }
spriteset (signals_shunt_white, "gfx/shunt.png") { tmpl_signals(40, 50) }
spriteset (signals_shunt_no_entry, "gfx/shunt.png") { tmpl_signals(10, 100) }

spriteset (signals_block_banner_on,       "gfx/banner.png") { tmpl_signals(10,  50) }
spriteset (signals_block_banner_off,      "gfx/banner.png") { tmpl_signals(40,  50) }
spriteset (signals_pbs_banner_on,         "gfx/banner.png") { tmpl_signals(10, 100) }
spriteset (signals_pbs_banner_off,        "gfx/banner.png") { tmpl_signals(40, 100) }
spriteset (signals_pbs_oneway_banner_on,  "gfx/banner.png") { tmpl_signals(10, 150) }
spriteset (signals_pbs_oneway_banner_off, "gfx/banner.png") { tmpl_signals(40, 150) }

spriteset (signals_combined_red,   "gfx/light-combined.png") { tmpl_signals_combined(10, 50) }
spriteset (signals_combined_green, "gfx/light-combined.png") { tmpl_signals_combined(40, 50) }
spriteset (signals_combined_y,     "gfx/light-combined.png") { tmpl_signals_combined(10, 100) }
spriteset (signals_combined_yy,    "gfx/light-combined.png") { tmpl_signals_combined(40, 100) }
spriteset (signals_combined_shunt, "gfx/light-combined.png") { tmpl_signals_combined(10, 150) }

spriteset (signals_combined_red_short,   "gfx/light-combined-short.png") { tmpl_signals_combined(10, 50) }
spriteset (signals_combined_green_short, "gfx/light-combined-short.png") { tmpl_signals_combined(40, 50) }
spriteset (signals_combined_y_short,     "gfx/light-combined-short.png") { tmpl_signals_combined(10, 100) }
spriteset (signals_combined_yy_short,    "gfx/light-combined-short.png") { tmpl_signals_combined(40, 100) }
spriteset (signals_combined_shunt_short, "gfx/light-combined-short.png") { tmpl_signals_combined(10, 150) }

spriteset (signals_combined_oneway_red,   "gfx/light-combined.png") { tmpl_signals_combined(10, 200) }
spriteset (signals_combined_oneway_green, "gfx/light-combined.png") { tmpl_signals_combined(40, 200) }
spriteset (signals_combined_oneway_y,     "gfx/light-combined.png") { tmpl_signals_combined(10, 250) }
spriteset (signals_combined_oneway_yy,    "gfx/light-combined.png") { tmpl_signals_combined(40, 250) }
spriteset (signals_combined_oneway_shunt, "gfx/light-combined.png") { tmpl_signals_combined(10, 300) }

spriteset (signals_combined_oneway_red_short,   "gfx/light-combined-short.png") { tmpl_signals_combined(10, 200) }
spriteset (signals_combined_oneway_green_short, "gfx/light-combined-short.png") { tmpl_signals_combined(40, 200) }
spriteset (signals_combined_oneway_y_short,     "gfx/light-combined-short.png") { tmpl_signals_combined(10, 250) }
spriteset (signals_combined_oneway_yy_short,    "gfx/light-combined-short.png") { tmpl_signals_combined(40, 250) }
spriteset (signals_combined_oneway_shunt_short, "gfx/light-combined-short.png") { tmpl_signals_combined(10, 300) }

switch(FEAT_SIGNALS, SELF, signal_switch_block_traditional, [
        STORE_TEMP(hasbit(extra_callback_info2, 24) ? param[10] : 0, 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_block_red;
    1: signals_block_green;
    param_aspect_style == 2 ? 3 : 2: signals_block_y;
    param_aspect_style == 2 ? 2 : 3: signals_block_yy;
}

switch(FEAT_SIGNALS, SELF, signal_switch_block_new, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_new_block_red;
    1: signals_new_block_green;
    param_aspect_style == 2 ? 3 : 2: signals_new_block_y;
    param_aspect_style == 2 ? 2 : 3: signals_new_block_yy;
}

switch (FEAT_SIGNALS, SELF, signal_switch_block, param_block_style) {
    0: signal_switch_block_traditional;
    1: signal_switch_block_new;
}

switch(FEAT_SIGNALS, SELF, signal_switch_pbs_std, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_pbs_red;
    1: signals_pbs_green;
    param_aspect_style == 2 ? 3 : 2: signals_pbs_y;
    param_aspect_style == 2 ? 2 : 3: signals_pbs_yy;
}

switch(FEAT_SIGNALS, SELF, signal_switch_pbs, signal_vertical_clearance < param[11]) {
    default: signal_switch_pbs_std;
    1: signal_switch_block_new;
}

switch(FEAT_SIGNALS, SELF, signal_switch_pbs_oneway, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF) | (signal_vertical_clearance < param[11] ? 0x100 : 0)
    ]) {
    0: signals_pbs_oneway_red;
    1: signals_pbs_oneway_green;
    param_aspect_style == 2 ? 3 : 2: signals_pbs_oneway_y;
    param_aspect_style == 2 ? 2 : 3: signals_pbs_oneway_yy;

    0x100: signals_pbs_oneway_red_short;
    0x101: signals_pbs_oneway_green_short;
    param_aspect_style == 2 ? 0x103 : 0x102: signals_pbs_oneway_y_short;
    param_aspect_style == 2 ? 0x102 : 0x103: signals_pbs_oneway_yy_short;
}

switch(FEAT_SIGNALS, SELF, signal_switch_shunt, [
        STORE_TEMP(hasbit(extra_callback_info2, 24) ? param[10] : 0, 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_shunt_red;
    signals_shunt_white;
}

switch(FEAT_SIGNALS, SELF, signal_switch_shunt_no_entry, [
        STORE_TEMP(hasbit(extra_callback_info2, 24) ? param[10] : 0, 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    signals_shunt_no_entry;
}

switch(FEAT_SIGNALS, SELF, signal_switch_banner_block, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_block_banner_on;
    2: signals_block_banner_on;
    signals_block_banner_off;
}

switch(FEAT_SIGNALS, SELF, signal_switch_banner_pbs, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_pbs_banner_on;
    2: signals_pbs_banner_on;
    signals_pbs_banner_off;
}

switch(FEAT_SIGNALS, SELF, signal_switch_banner_pbs_oneway, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF)
    ]) {
    0: signals_pbs_oneway_banner_on;
    2: signals_pbs_oneway_banner_on;
    signals_pbs_oneway_banner_off;
}

switch(FEAT_SIGNALS, SELF, signal_switch_combined_pbs, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF) | (signal_vertical_clearance < param[11] ? 0x100 : 0)
    ]) {
    0: signals_combined_red;
    1: signals_combined_green;
    param_aspect_style == 2 ? 3 : 2: signals_combined_y;
    param_aspect_style == 2 ? 2 : 3: signals_combined_yy;
    0xFF: signals_combined_shunt;

    0x100: signals_combined_red_short;
    0x101: signals_combined_green_short;
    param_aspect_style == 2 ? 0x103 : 0x102: signals_combined_y_short;
    param_aspect_style == 2 ? 0x102 : 0x103: signals_combined_yy_short;
    0x1FF: signals_combined_shunt_short;
}

switch(FEAT_SIGNALS, SELF, signal_switch_combined_pbs_oneway, [
        STORE_TEMP(param[10] + (hasbit(extra_callback_info2, 24) ? 2 : 1), 0x100),
        (extra_callback_info2 & 0x000000FF) | (signal_vertical_clearance < param[11] ? 0x100 : 0)
    ]) {
    0: signals_combined_oneway_red;
    1: signals_combined_oneway_green;
    param_aspect_style == 2 ? 3 : 2: signals_combined_oneway_y;
    param_aspect_style == 2 ? 2 : 3: signals_combined_oneway_yy;
    0xFF: signals_combined_oneway_shunt;

    0x100: signals_combined_oneway_red_short;
    0x101: signals_combined_oneway_green_short;
    param_aspect_style == 2 ? 0x103 : 0x102: signals_combined_oneway_y_short;
    param_aspect_style == 2 ? 0x102 : 0x103: signals_combined_oneway_yy_short;
    0x1FF: signals_combined_oneway_shunt_short;
}

switch(FEAT_SIGNALS, SELF, switch_signals, (extra_callback_info2 & 0x00FFFF00) | signal_style) {
        0x0: signal_switch_block;        //light, block
    0x40000: signal_switch_pbs;          //light, PBS
    0x50000: signal_switch_pbs_oneway;   //light, PBS oneway
    0x40001: signal_switch_shunt;        //shunt
    0x40002: signal_switch_shunt;        //shunt (opposite)
    0x70001: signal_switch_shunt_no_entry; //shunt no entry
    0x70002: signal_switch_shunt_no_entry; //shunt no entry (opposite)
        0x3: signal_switch_banner_block; //banner, block
    0x40003: signal_switch_banner_pbs;   //banner, PBS
    0x50003: signal_switch_banner_pbs_oneway; //banner, PBS oneway
    0x40004: signal_switch_combined_pbs;        //combined, PBS
    0x50004: signal_switch_combined_pbs_oneway; //combined, PBS oneway
    CB_FAILED;
}

item (FEAT_SIGNALS, custom_signals, 0) {
    property {
        enable_restricted_signals: param_routing_restriction;
        enable_signal_recolour: 1;
        extra_aspects: param_aspect_style > 0 ? 2 : 1;
    }

    graphics {
        switch_signals;
    }
}

if (param_custom_styles) {
    item (FEAT_SIGNALS, custom_signals, 0) {
        property {
            define_style: 1;
            style_name: string(STR_STYLE_SHUNT);
            style_electric_enabled: 0x90;
            style_no_aspect_increase: 1;
            style_lookahead_single_signal_only: 1;

            define_style: 2;
            style_name: string(STR_STYLE_SHUNT_OPPOSITE);
            style_electric_enabled: 0x90;
            style_no_aspect_increase: 1;
            style_lookahead_single_signal_only: 1;
            style_opposite_side: 0x1;

            define_style: 3;
            style_name: string(STR_STYLE_BANNER);
            style_no_aspect_increase: 1;
            style_always_reserve_through: 1;
            style_lookahead_extra_aspects: 1;
            style_electric_enabled: 0x31;
            style_realistic_braking_only: 1;

            define_style: 4;
            style_name: string(STR_STYLE_COMBINED);
            style_combined_normal_shunt: 1;
            style_electric_enabled: 0x30;
            style_realistic_braking_only: 1;
        }
    }
}
